buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.jenkins-ci.org/releases/'
        }
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.18.1'
    }
}

defaultTasks 'clean', 'jpi'

/**
 === Usage ===

 gradlew jpi - Build the Jenkins plugin file, which can then be found in the build directory. The file will currently end in ".hpi".
 gradlew publishToMavenLocal - Build the Jenkins plugin and install it into your local Maven repository.
 gradlew publish - Deploy your plugin to the Jenkins Maven repository to be included in the Update Center.
 gradlew server - Start a local instance of Jenkins (http://localhost:8080) with the plugin pre-installed for testing and debugging.
 */

ext.enforceJavaVersion = 1.7

apply plugin: 'org.jenkins-ci.jpi'
apply from: "$rootDir/config/gradle/common.gradle"
apply from: "$rootDir/config/gradle/staticCodeAnalysis.gradle"

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://repo.jenkins-ci.org/releases/'
    }
}

ext."jenkins.httpPort" = 8080

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

// Integration Test Structure
test {
    exclude '**/integration/**'
    reports {
        junitXml.enabled = false
        html.enabled = false
    }
}

task startJenkins(type: JenkinsServerTask, dependsOn: [jpi, test]) {}

task integTest(type: Test, dependsOn: startJenkins) {
    testClassesDir = sourceSets.test.output.classesDir
    classpath = sourceSets.test.runtimeClasspath
    include '**/integration/**'
    outputs.upToDateWhen { false }
    reports {
        junitXml.enabled = false
        html.enabled = false
    }
}

check.dependsOn integTest

jenkinsPlugin {
    // always use latest LTS!
    coreVersion = '2.7.4'
    shortName = project.name
    displayName = jenkinsDisplayName
    url = "https://wiki.jenkins-ci.org/display/JENKINS/$jenkinsDisplayName"
    gitHubUrl = "https://github.com/jenkinsci/$project.name"
    compatibleSinceVersion = '1.20'
    fileExtension = 'hpi'
    maskClasses = 'groovy.grape'
    developers {
        developer {
            id 'nickg'
            name 'Nick Grealy'
            email 'nickgrealy@gmail.com'
        }
    }
}

configurations { forceGroovy }

dependencies {
    forceGroovy "org.codehaus.groovy:groovy-all:2.4.7"
    compile 'org.apache.ivy:ivy:2.4.0' // For @Grab

    testCompile('org.hamcrest:hamcrest-core:1.3') { force = true }
    testCompile('junit:junit:4.12') { force = true }
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile "info.cukes:cucumber-junit:1.2.4"
    testCompile "info.cukes:cucumber-java:1.2.4"
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"
}

task deleteTarget(type: Delete) {
    delete 'target', 'out', 'work'
}

clean.dependsOn deleteTarget

jpi {
    classpath configurations.forceGroovy // add the Groovy lib to the plugin to make @Grab work
    manifest.attributes(["PluginFirstClassLoader": "true"])
}
jpi.outputs.upToDateWhen { false }
jpi.doLast {
    verifySizeKb(file(jpi.archivePath), 7307)
}

/**
 * We must exclude the junit-dep dependency, because it is clashing with junit when we run the AcceptanceTests.
 */
configurations.findAll { !it.name.endsWith('junit-dep') }.each { conf ->
    conf.exclude group: "junit", module: "junit-dep"
}

findbugs { sourceSets = [sourceSets.main] }

configurations.runtime {
    resolutionStrategy {
        force 'commons-codec:commons-codec:1.8'
        force 'org.jenkins-ci:trilead-ssh2:build217-jenkins-8'
        force 'commons-io:commons-io:2.4'
        force 'org.codehaus.groovy:groovy-all:2.4.7'
        force 'org.kohsuke.stapler:stapler:1.243'
        force 'org.jenkins-ci:annotation-indexer:1.11'
        force 'commons-beanutils:commons-beanutils:1.8.3'
        force 'commons-lang:commons-lang:2.6'
        force 'commons-logging:commons-logging:1.1.1'
        force 'commons-collections:commons-collections:3.2.1'
        force 'jaxen:jaxen:1.1-beta-11'
        force 'org.springframework:spring-core:2.5.6.SEC03'
        force 'net.java.dev.jna:jna:4.2.1'
        force 'org.jvnet.localizer:localizer:1.23'
        force 'com.google.guava:guava:14.0'
        force 'com.jcraft:jzlib:1.1.3'
        force 'org.springframework:spring-beans:2.5.6.SEC03'
        force 'org.springframework:spring-context:2.5.6.SEC03'
        force 'commons-fileupload:commons-fileupload:1.3.1-jenkins-1'
    }
}


import java.nio.file.Files
import java.util.jar.JarFile

/**
 * Task that starts Jenkins in place with the current plugin.
 *
 * @author Kohsuke Kawaguchi
 */
class JenkinsServerTask extends DefaultTask {
    private static final String HTTP_PORT = 'jenkins.httpPort'

    @TaskAction
    def start() {
        def c = project.configurations.getByName(org.jenkinsci.gradle.plugins.jpi.JpiPlugin.WAR_DEPENDENCY_CONFIGURATION_NAME)
        def files = c.resolve()
        if (files.isEmpty()) {
            throw new GradleException('No jenkins.war dependency is specified')
        }
        File war = files.toArray()[0]

        generateHpl()

        def conv = project.extensions.getByType(org.jenkinsci.gradle.plugins.jpi.JpiExtension)
        System.setProperty('JENKINS_HOME', conv.workDir.absolutePath)
        setSystemPropertyIfEmpty('stapler.trace', 'true')
        setSystemPropertyIfEmpty('stapler.jelly.noCache', 'true')
        setSystemPropertyIfEmpty('debug.YUI', 'true')

        List<String> args = []
        String port = project.properties[HTTP_PORT] ?: System.properties[HTTP_PORT]
        println "Using port: $port"
        if (port) {
            args << "--httpPort=${port}"
        }

        def cl = new URLClassLoader([war.toURI().toURL()] as URL[])
        def mainClass = new JarFile(war).manifest.mainAttributes.getValue('Main-Class')
        cl.loadClass(mainClass).main(args as String[])
    }

    private static void generateHpl() {
        def from = new File("build/libs/groovy-events-listener-plugin.hpi")
        def to = new File("work/plugins/groovy-events-listener-plugin.hpi")
        assert [from.exists(), to.parentFile.mkdirs()].every()
        Files.copy(from.toPath(), to.toPath())
        println ">>> Copied file from '$from' to '$to'"
    }

    private static void setSystemPropertyIfEmpty(String name, String value) {
        if (!System.getProperty(name)) {
            System.setProperty(name, value)
        }
    }
}