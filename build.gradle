plugins {
    //id 'org.jenkins-ci.jpi' version '0.38.0'
    id 'org.jenkins-ci.jpi' version '0.36.2'
    id 'jacoco'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    throw new GradleException("Build requires Java ${JavaVersion.VERSION_1_8}")
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/releases'
    }
    maven {
        url 'https://repo.jenkins-ci.org/public'
    }    
    maven {
        url "https://maven.springframework.org/release"
    } 
    maven {
      url 'http://repo.mycompany.com/maven2'
    }
}

jenkinsPlugin {
    //coreVersion = '2.138.4'
    coreVersion = '2.143'
    shortName = project.name
    displayName = jenkinsDisplayName
    url = "https://github.com/jenkinsci/$project.name"
    gitHubUrl = "https://github.com/jenkinsci/$project.name"
    compatibleSinceVersion = '1.20'
    fileExtension = 'hpi'
    maskClasses = 'groovy.grape'
    pluginFirstClassLoader = true
    developers {
        developer {
            id 'nickg'
            name 'Nick Grealy'
            email 'nickgrealy@gmail.com'
        }
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
    tasks.withType(GroovyCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

configurations { forceGroovy }

dependencies {
    forceGroovy 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'org.apache.ivy:ivy:2.4.0' // For @Grab
    compile 'com.cloudbees:groovy-cps:1.7@jar'

    testCompile 'io.cucumber:cucumber-junit:4.8.1'
    testCompile 'io.cucumber:cucumber-java:4.8.1'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version: '2.9', ext: 'jar'
    compile group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-scm-step', version: '2.10', ext: 'jar'

    jenkinsPlugins 'org.jenkins-ci.plugins:matrix-project:1.14@jar'
    jenkinsPlugins 'org.jenkins-ci.plugins:junit:1.28@jar'
    
    // Test dependencies
    jenkinsTest "org.jenkins-ci.plugins:ant:1.3@jar"
    jenkinsTest "org.mockito:mockito-all:1.8.5@jar"
    jenkinsTest 'org.jenkins-ci.plugins:script-security:1.19@jar'
    jenkinsTest 'org.jenkins-ci.main:maven-plugin:2.12.1@jar'
    jenkinsTest 'org.jenkins-ci.plugins:javadoc:1.3@jar'
    jenkinsTest 'org.jenkins-ci.plugins:mailer:1.17@jar'
        
    // Test dependencies for pipeline plugin
    jenkinsTest 'org.jenkins-ci.main:jenkins-test-harness:2.59'    
    jenkinsTest "org.jenkins-ci.plugins:durable-task:1.33@jar"
    
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-aggregator:2.6@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-api:2.40@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-basic-steps:2.19@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-cps:2.80@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:2.15@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-durable-task-step:2.35@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-job:2.37@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-multibranch:2.21@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-scm-step:2.10@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-step-api:2.22@jar"
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-support:3.4@jar"
        
    //jenkinsPlugins 'org.jenkins-ci.plugins:structs'
    //jenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-api'
    //jenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps'
    //jenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-job'
    //jenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-basic-steps'
    //jenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-durable-task-step'
    //jenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api'
}

defaultTasks 'clean', 'jpi'

clean {
    delete 'work'
    delete '.gradle'
}

tasks.register('deleteTarget', Delete) {
    delete 'target', 'out', 'work'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

// Integration Test Structure
test {
    exclude '**/integration/**'
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

tasks.register('integTest', Test) {
    dependsOn = [jpi, test]
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include '**/integration/**'
    outputs.upToDateWhen { false }
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

check.dependsOn 'integTest'

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }
}

clean.dependsOn deleteTarget

war {
    classpath configurations.forceGroovy // add the Groovy lib to the plugin to make @Grab work
}

wrapper {
    gradleVersion = '6.0.1'
}
